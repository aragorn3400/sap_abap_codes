"E-Mail versenden (XLS-Anhang)

DATA: go_gbt      TYPE REF TO cl_gbt_multirelated_service,
      go_bcs      TYPE REF TO cl_bcs,
      go_doc_bcs  TYPE REF TO cl_document_bcs,
      go_recipient TYPE REF TO if_recipient_bcs,
      gt_soli     TYPE TABLE OF soli,
      gs_soli     TYPE         soli,
      gv_status   TYPE         bcs_rqst,
      gv_content  TYPE         string.


DATA:   gv_attachment_size    TYPE  sood-objlen,
        gt_att_content_hex    TYPE  solix_tab,
        gv_att_content TYPE string,
        gv_att_line TYPE string.

START-OF-SELECTION.

CREATE OBJECT go_gbt.

SELECT * FROM scarr INTO TABLE @DATA(gt_scarr).

gv_content =
          '<!DOCTYPE html>                             '
        &&'<html>                                      '
        &&'<head>                                      '
        &&'     <meta charset=¨utf-8¨>                 '
        &&'         <style>                            '
        &&'	th {background-color:pink ;                '
        &&'         border:2px solid;                  '
        &&'           }                                '
        &&'	td   {background-color:lightgreen ;        '
        &&'         border:1px solid;                  '
        &&'           }                                '
        &&'       body { background-color:powderblue;  '
        &&'         }                                  '
        &&'	                                           '
        &&'	</style>                                   '
        &&'</head>                                     '
        &&'<body>                                      '
        &&'	<table>                                    '
        &&'      <tr>                               '
        &&'         <th>Airlines Short Descriptiopn</th>               '
        &&'             <th>Airlines Name</th>               '
        &&'             <th>Currency</th>               '
        &&'             <th>URL</th>               '
        &&'           </tr>                            '.

   LOOP AT gt_scarr INTO DATA(gs_scarr).
     gv_content = gv_content &&  ' <tr>                '
      && ' 	<td>' && gs_scarr-carrid    && '</td>   '
      && ' 	<td>' && gs_scarr-carrname    && '</td>   '
      && ' 	<td>' && gs_scarr-currcode    && ' </td>   '
      && ' 	<td>' && gs_scarr-url    && '</td>   '
      && ' </tr>               '.

   ENDLOOP.

  gv_content = gv_content && '</table>'
                          && '</body>'
                          && '</html>'.

gt_soli = cl_document_bcs=>string_to_soli( ip_string = gv_content  ).

CALL METHOD go_gbt->set_main_html
  EXPORTING
    content     = gt_soli .

go_doc_bcs = cl_document_bcs=>create_from_multirelated(
                 i_subject          = 'Scarr Table  '                   "Başlık yazılır
                 i_multirel_service = go_gbt ) .

LOOP AT gt_scarr INTO gs_scarr.
  CONCATENATE  gs_scarr-carrid
               gs_scarr-carrname
               gs_scarr-currcode
               gs_scarr-url
               INTO gv_att_line
               SEPARATED BY cl_abap_char_utilities=>horizontal_tab.
  IF sy-tabix EQ 1.
    gv_att_content = gv_att_line.
  ELSE.
    CONCATENATE gv_att_content
                gv_att_line
                INTO gv_att_content
                SEPARATED BY cl_abap_char_utilities=>newline.
   ENDIF.

ENDLOOP.

cl_bcs_convert=>string_to_solix(
  EXPORTING
    iv_string   = gv_att_content
    iv_codepage =  '4103'             "Sabit,karakter için kod değeri.Farklı karakter kümeleri için farklı kodlar var.
    iv_add_bom  =    'X'
  IMPORTING
    et_solix    =   gt_att_content_hex
    ev_size     =   gv_attachment_size
).
*  CATCH cx_bcs.    "

go_doc_bcs->add_attachment(
  EXPORTING
    i_attachment_type     = 'xls'                       "Sabit kalacak,gönderilmek istenen tablo uzantısı
    i_attachment_subject  = 'scarr_table'              "Tablo ismi,değiştirebilirsin
    i_attachment_size     =  gv_attachment_size
    i_att_content_hex     =  gt_att_content_hex ).

go_recipient = cl_cam_address_bcs=>create_internet_address(
                   i_address_string = 'test012312@gmail.com ' ).  "Alıcı yazılır

go_bcs = cl_bcs=>create_persistent( ).
go_bcs->set_document( i_document = go_doc_bcs ).
go_bcs->add_recipient( i_recipient = go_recipient ).

gv_status = 'N'.
CALL METHOD go_bcs->set_status_attributes
  EXPORTING
    i_requested_status = gv_status .

go_bcs->send( ).
COMMIT WORK.
